apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

buildscript {
    repositories {
        jcenter()
    }
}

def libraryVersionName = '1.2.1'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions.unitTests.includeAndroidResources = true

    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    libraryVariants.all {
        it.generateBuildConfigProvider.configure { task ->
            task.enabled = false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.2'

    implementation 'androidx.core:core-ktx:1.3.2'

    testImplementation "androidx.test.ext:junit:1.1.1"
    testImplementation 'androidx.test:rules:1.2.0'
    testImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'

    implementation 'me.grantland:autofittextview:0.2.1'
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
    }
}
